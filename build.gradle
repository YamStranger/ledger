buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:5.1.0'
    }
}

plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.3.41'
    id 'com.github.johnrengelman.shadow' version '5.1.0'
}

apply from: 'ktlint.gradle'

version '1.0.0'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
    mavenLocal()
    jcenter()
}

configurations.all {
    resolutionStrategy {
        failOnVersionConflict()
    }
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
        jvmTarget = "1.8"
        allWarningsAsErrors = true
    }
}

test {
    useJUnitPlatform()
    testLogging {
        exceptionFormat = 'full'
    }
}

dependencies {
    def kotlinVersion = plugins.findPlugin("org.jetbrains.kotlin.jvm").kotlinPluginVersion
    api(group: 'org.jetbrains.kotlin', name: 'kotlin-stdlib-jdk8', version: "${kotlinVersion}")
    api(group: 'com.typesafe', name: 'config', version: '1.3.4')
    api(group: 'com.google.inject', name: 'guice', version: '4.2.2', classifier: 'no_aop')
    api(group: 'dev.misfitlabs.kotlinguice4', name: 'kotlin-guice', version: '1.4.0')
    api(group: 'io.undertow', name: 'undertow-core', version: '2.0.1.Final')
    api(group: 'com.google.code.gson', name: "gson", version: "2.8.5")

    api(group: 'io.github.microutils', name: 'kotlin-logging', version: '1.5.4') {
        exclude group: "org.jetbrains.kotlin"
    }

    api(group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.3')
    api(group: 'ch.qos.logback', name: 'logback-core', version: '1.2.3')

    testImplementation(group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.3.1')
    testRuntimeOnly(group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.3.1')
    testImplementation(group: 'com.github.kittinunf.fuel', name: 'fuel', version: '2.2.0')
    testImplementation(group: 'com.github.kittinunf.fuel', name: 'fuel-gson', version: '2.2.0')
    testImplementation(group: 'org.assertj', name: 'assertj-core', version: '3.10.0')
    testImplementation(group: 'io.mockk', name: 'mockk', version: '1.9.3')

    components.all {
        allVariants {
            withDependencies { deps ->
                deps.each { dep ->
                    if (dep.group == 'org.jetbrains.kotlin' &&
                            (dep.name == 'kotlin-reflect' || dep.name == 'kotlin-stdlib')) {
                        dep.version {
                            require '1.3.41'
                        }
                        dep.because "Kotlin dependecy should be '1.3.41'"
                    }
                }
            }
        }
    }
}

shadowJar {
    manifest {
        attributes 'Main-Class': 'com.reut.ledger.MainKt'
    }
}
